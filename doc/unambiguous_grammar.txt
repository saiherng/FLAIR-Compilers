----------------------------------------------------------------------------
	<PROGRAM> ::= program <IDENTIFIER> ( <FORMALS> ) ;
                             <DEFINITIONS>
                             <BODY> .
----------------------------------------------------------------------------
        <DEFINITIONS> ::= ε
                        | <DEF> <DEFINITIONS>

----------------------------------------------------------------------------
                <DEF> ::= function <IDENTIFIER> ( <FORMALS> ) : <TYPE>
                             <BODY> ;
----------------------------------------------------------------------------
            <FORMALS> ::= ε
                        | <NONEMPTYFORMALS>

----------------------------------------------------------------------------

        <NONEMPTYFORMALS> ::= <FORMAL> <NONEMPTY-F-REST>

        <NONEMPTY-F-REST> ::= , <NONEMPTYFORMALS>
                            | Ɛ
----------------------------------------------------------------------------
	<FORMAL> ::= <IDENTIFIER> : <TYPE>
----------------------------------------------------------------------------

	<BODY> ::= begin <STATEMENT-LIST> end
----------------------------------------------------------------------------

     <STATEMENT-LIST> ::= <PRINT-STATEMENT> <STATEMENT-LIST>
                        | return <EXPR>

----------------------------------------------------------------------------
               <TYPE> ::= integer
                        | boolean
----------------------------------------------------------------------------

        <EXPR> ::= <SIMPLE-EXPR> <E-TAIL>

        <E-TAIL> ::= < <EXPR>
                   | = <EXPR>
                   | Ɛ

----------------------------------------------------------------------------
     <SIMPLE-EXPR> ::= <TERM> <SE-TAIL> 

     <SE-TAIL> ::= or <SIMPLE-EXPR>
                | +  <SIMPLE-EXPR>
                | -  <SIMPLE-EXPR>
                | Ɛ

----------------------------------------------------------------------------
	<TERM> ::= <FACTOR> <T-TAIL>
	<T-TAIL> ::= and <TERM>
		| * <TERM>
		| / <TERM>
		| Ɛ

----------------------------------------------------------------------------
     <FACTOR> ::= if <EXPR> then <EXPR> else <EXPR>
                | not <FACTOR>
                | <ID>
                | <LITERAL>
                | - <FACTOR>
                | ( <EXPR> )

         <ID> ::=  <IDENTIFIER> <ID-REST>
                
        <ID-REST> ::= ( <ACTUALS> ) 
                    | Ɛ 
----------------------------------------------------------------------------


  	<ACTUALS> ::= ε
                    | <NONEMPTYACTUALS>

----------------------------------------------------------------------------

     <NONEMPTYACTUALS> ::= <EXPR> <NONEMPTY-A-REST>

      <NONEMPTY-A-REST> ::=  , <NONEMPTYACTUALS>
                        |  Ɛ
----------------------------------------------------------------------------
                       
	<LITERAL> ::= <NUMBER>
                    | <BOOLEAN>
----------------------------------------------------------------------------

    <PRINT-STATEMENT> ::= print ( <EXPR> ) ;

----------------------------------------------------------------------------


