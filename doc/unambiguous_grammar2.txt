----------------------------------------------------------------------------
1.	<PROGRAM> ::= program <IDENTIFIER> ( <FORMALS> ) ;
                             <DEFINITIONS>
                             <BODY> .
----------------------------------------------------------------------------
2.        <DEFINITIONS> ::= e
3.                        | <DEF> <DEFINITIONS>

----------------------------------------------------------------------------
4.                <DEF> ::= function <IDENTIFIER> ( <FORMALS> ) : <TYPE>
                            <BODY> ;
----------------------------------------------------------------------------
5.            <FORMALS> ::= e
6.                        | <NONEMPTYFORMALS>

----------------------------------------------------------------------------

7.        <NONEMPTYFORMALS> ::= <FORMAL> <NONEMPTY-F-REST>

8.        <NONEMPTY-F-REST> ::= , <FORMAL> <NONEMPTY-F-REST>
9.                            | e
----------------------------------------------------------------------------
10.	<FORMAL> ::= <IDENTIFIER> : <TYPE>
----------------------------------------------------------------------------

11.	<BODY> ::= begin <STATEMENT-LIST> end
----------------------------------------------------------------------------

12.     <STATEMENT-LIST> ::= <PRINT-STATEMENT> <STATEMENT-LIST>
13.                        | return <EXPR>

----------------------------------------------------------------------------
14.               <TYPE> ::= integer
15.                        | boolean
----------------------------------------------------------------------------

16.        <EXPR> ::= <SIMPLE-EXPR> <E-TAIL>

17.        <E-TAIL> ::= < <SIMPLE-EXPR> <E-TAIL>
18.                   | = <SIMPLE-EXPR> <E-TAIL>
19.                   | e

----------------------------------------------------------------------------
20.     <SIMPLE-EXPR> ::= <TERM> <SE-TAIL> 

21.     <SE-TAIL> ::= or <TERM> <SE-TAIL>
22.                | +  <TERM> <SE-TAIL>
23.                | -  <TERM> <SE-TAIL>
24.                | e

----------------------------------------------------------------------------
25.	<TERM> ::= <FACTOR> <T-TAIL>
26.	<T-TAIL> ::= and <FACTOR> <T-TAIL>
27.		   | * <FACTOR> <T-TAIL>
28.		   | / <FACTOR> <T-TAIL>
29.		   | e

----------------------------------------------------------------------------
30.     <FACTOR> ::= if <EXPR> then <EXPR> else <EXPR>
31.                | not <FACTOR>
32.                | <ID>
33.                | <LITERAL>
34.                | - <FACTOR>
35.                | ( <EXPR> )

36.         <ID> ::=  <IDENTIFIER> <ID-REST>
                
37.        <ID-REST> ::= ( <ACTUALS> ) 
38.                    | e 
----------------------------------------------------------------------------
39.  	<ACTUALS> ::= e
40.                 | <NONEMPTYACTUALS>

----------------------------------------------------------------------------

41.     <NONEMPTYACTUALS> ::= <EXPR> <NONEMPTY-A-REST>

42.      <NONEMPTY-A-REST> ::=  , <NONEMPTYACTUALS>
43.                          |  e
----------------------------------------------------------------------------
                       
44.	<LITERAL> ::= <NUMBER>
45.                 | <BOOLEAN>
----------------------------------------------------------------------------

46.    <PRINT-STATEMENT> ::= print ( <EXPR> ) ;

----------------------------------------------------------------------------


